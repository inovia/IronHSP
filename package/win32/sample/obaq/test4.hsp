#include "obaq.as"

	;
	;	一定時間ごとに指定数のオブジェクトを追加
	;	[ENTER]キーで全オブジェクトがジャンプします
	;	[ESC]で終了します
	;
	randomize		; 乱数の初期化
	screen 0,640,480	; ウィンドウ初期化
	celload "obaqlogo.bmp"	; OBAQロゴ読み込み
	logoid=stat		; ロゴのID

	qreset			; OBAQの初期化
	kazu=25			; 出現数
	frame=0
*main
	;	メインループ
	;
	redraw 0		; 画面の更新を開始
	gradf ,,,,1,0,128	; 画面クリア
	qexec			; OBAQによるオブジェクトの更新

	stick key,15		; キーの取得
	if key&128 : end	; [ESC]キーで終了
	if key&32 {		; [ENTER]キーで全オブジェクトジャンプ
		qfind 1		; オブジェクト検索
		repeat
		qnext i:if i<0 : break
		qspeed i,0,-0.5	; ランダムな方向に飛ばす
		loop
	}

	;	一定時間ごとに出現
	;
	if frame\30=0 {
		if kazu>0 {
			qaddpoly my, rnd(3)+3, 96,24,0.01*rnd(628)	; 多角形を追加
			if my>=0 {
				qspeed my,0.1*rnd(10)-0.5,0,0.01	; ランダムな方向に飛ばす
				kazu--
			}
		}
	}

	qdraw			; オブジェクトの描画

	color 255,255,255
	pos 0,0:mes "Frame:"+frame	; フレーム数を表示

	gmode 2:pos 470,40	; ロゴ表示
	celput logoid

	redraw 1		; 画面の更新を終了
	await 12		; 一定時間待つ
	frame++
	goto *main
