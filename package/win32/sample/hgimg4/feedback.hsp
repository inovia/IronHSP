#include "hgimg4.as"

title "HGIMG4 Feedback Test"

	;	オフスクリーンバッファを使ったフィードバック描画テスト
	;
	gpreset

	setcls CLSMODE_SOLID, 0		; 画面クリア設定

	setpos GPOBJ_CAMERA, 0,0,3	; カメラ位置を設定

	gpbox id_model, 1		; 箱ノードを追加

	sx=ginfo_sx:sy=ginfo_sy
	buffer 1,sx,sy,screen_offscreen	; オフスクリーンバッファを初期化
	buffer 2,sx,sy,screen_offscreen	; オフスクリーンバッファを初期化

	bufid=1				; 描画先のID
	oldbuf=-1			; 以前の描画先ID
*main
	;	サブ画面
	gsel bufid
	redraw 0			; 描画開始

	if oldbuf>=0 {
		pos 0,0:gmode 3,sx,sy,254
		gcopy oldbuf		; 以前の画面を表示
	}

	addang id_model,0,0.02,0.01	; ノード回転
	gpdraw				; シーンの描画

	redraw 1			; 描画終了

	;	メイン画面
	gsel 0
	redraw 0

	pos 0,0:gmode 0
	celput bufid			; オリジナルを表示

	color 255,255,255
	pos 0,0:mes "HGIMG4 sample"

	redraw 1			; 描画終了
	await 1000/60			; 待ち時間

	;	入力チェック
	stick key
	if key&128 : goto *bye

	oldbuf=bufid
	bufid++
	if bufid>2 : bufid=1

	goto *main

*bye
	end
