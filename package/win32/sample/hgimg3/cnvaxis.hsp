#include "hgimg3.as"

	;	3Dモデルの中心座標を2D画面上の座標に変換する
	;
	screen 0,640,480,0
	cls 4
	hgini

	;	テクスチャフォント表示の準備
	;
	setfont 16,16,12,1	; font Tex select(cx,cy,px,mode)
	texload "fontchr.bmp"	; フォントテクスチャの登録

	;	BOXモデル登録
	;
	addbox m_model,10,10
	regobj obj, m_model		; オブジェクトの登録
	clscolor $80			; 背景色の設定

*main
	stick key,$3ff
	if key&128 : goto *owari

	hgdraw				; 描画

	getpos obj,x,y,z
	hgcnvaxis xx,yy,zz,x,y,z

	color 255,255,255:gmode 0
	hgline 0,0,xx,yy

	fprt "AXIS("+xx+","+yy+")",xx,yy

	hgsync 10			; 時間待ち

	;	カーソルキーで動かす
	;
	if key&1 : addpos obj, -0.2, 0.0
	if key&4 : addpos obj, 0.2, 0.0
	if key&64 {
		if key&8 : addpos obj, 0,0, 0.2
		if key&2 : addpos obj, 0,0, -0.2
	} else {
		if key&8 : addpos obj, 0.0, 0.2
		if key&2 : addpos obj, 0.0, -0.2
	}
	;	モデルを回転させる
	addang obj,0,0.05,0

	goto *main

*owari
	end
