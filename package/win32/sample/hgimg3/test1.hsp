#include "hgimg3.as"

	;	立方体をキーで回転させるサンプル
	;

	;	ウインドゥサイズの設定
	;
	screen 0,640,480,0
	cls 4

	;	初期設定
	;
	hgini

	;	BOXオブジェクトを登録
	;
	setsizef 8.0, 8.0	; BOXのXYZサイズ
	setuv 0,0,127,127	; テクスチャUV座標
	addbox mdid		; BOXモデルを作成
	texload "btex.bmp"	; テクスチャの登録
	regobj mychr,mdid	; BOXモデルをオブジェクトとして登録

	;	カメラ位置設定
	;
	selcpos
	objset3 0.0, 0.0, 20.0

*main
	;	描画メイン
	;
	hgdraw				; 描画処理
	hgsync 10			; 時間待ち

	stick k,127
	if k&128 : goto *owari		; [ESC]で終了

	;	キー操作で回転
	;	(スペースキー押しで移動)
	;
	if k&16 {
		selpos mychr
		if k&2 : objadd3 0.0, -0.05, 0.0
		if k&8 : objadd3 0.0, 0.05, 0.0
		if k&4 : objadd3 0.05, 0.0, 0.0
		if k&1 : objadd3 -0.05, 0.0, 0.0
	} else {
		selang mychr
		if k&1 : objadd3 0.0, -0.05, 0.0
		if k&4 : objadd3 0.0, 0.05, 0.0
		if k&2 : objadd3 0.05, 0.0, 0.0
		if k&8 : objadd3 -0.05, 0.0, 0.0
	}

	goto *main

*owari
	end

