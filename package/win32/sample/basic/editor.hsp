
;  mesbox命令による簡易テキストエディタ (HSP3用)
;  (Windows9xで編集可能なテキストファイルサイズは32K程度まで)
;

	cls 1
	objmode 1
	sdim buf,65000			; テキストのバッファを確保
	buf=""				; バッファをクリア
	flg=1				; 0=編集不可/1=編集可能
	mesbox buf,636,446,flg+4,0	; 最大サイズ編集許可

	objsize 60,24
	pos 410,450:button "LOAD",*file_load
	pos 480,450:button "SAVE",*file_save
	pos 548,450:button "END",*endbtn

	size=0
	onkey goto *keychk
*main
	stop

*keychk
	title "size="+strlen(buf)	; テキストサイズをタイトルに表示
	goto *main

*file_save
	dialog "txt",17,"ﾃｷｽﾄﾌｧｲﾙ"
	if stat=0 : goto *dlcan
	fname = refstr
	exname=getpath(fname,2)		; ファイル名の拡張子を取得
	if exname="" : fname+=".txt"	; 拡張子がなければ.txtを追加
	notesel buf
	notesave fname			; テキストファイルをセーブ
*dlcan
	stop

*file_load
	dialog "txt",16,"ﾃｷｽﾄﾌｧｲﾙ"
	if stat=0 : goto *dlcan2
	notesel buf
	noteload refstr			; テキストファイル読み込み
	objprm 0,buf
*dlcan2
	stop

*endbtn
	end
