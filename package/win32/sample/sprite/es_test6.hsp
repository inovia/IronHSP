#include "hsp3dish.as"

	;
	;	HSP3Dish sprite sample6
	;		スプライト表示割り込みのテスト
	;		ゆっくりマウスカーソルを追いかけます
	;		[ESC]キーで終了
	;
	;------------------------------------------------------------------

	randomize
	wx=640:wy=480
	screen 0,wx,wy				; スクリーン初期化
	es_ini					; スプライト初期化

*start
	buffer 3
	picload "testchr.png"
	es_size 64,64,50
	es_pat 0, 0,0			; 顔
	es_pat 1, 64,0			; リンゴ

	gsel 0

	repeat 30
	x=rnd(640):y=rnd(480)
	es_set cnt,x,y,0
	es_setgosub cnt,*sp_sub		; スプライト表示後に*sp_subを呼び出す
	loop

*main
	myposx = mousex
	myposy = mousey
	stick ky,$1f				; カーソルキーの情報を取得
	if ky&$80 : goto *owari

	redraw 0
	rgbcolor 0:boxf
	es_draw					; スプライト描画

	redraw 1
	await 1000/60				; タスク更新

	goto *main

*sp_sub
	;	スプライト描画後の割り込み
	es_aim iparam,myposx,myposy,50		; マウスカーソルを追いかける
	return

*owari
	es_bye
	end


